PREFIX dbc: <http://dbpedia.org/resource/Category:>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Rank publicly-traded companies headquartered in Columbus Ohio by revenue

SELECT ?company ?label ?symbol ?revenueBillions
WHERE {
  ?company dct:subject dbc:Companies_based_in_the_Columbus\,_Ohio_metropolitan_area;
                   dbp:type dbr:Public_company;
                   rdfs:label ?label;
                   dbp:symbol ?symbol;
                   dbo:revenue ?r.
   FILTER langMatches(lang(?label),"en")
   FILTER NOT EXISTS {?company dbo:dissolutionDate|dbp:defunct ?x.}
   BIND(xsd:float(?r)/1000000000 as ?revenueBillions)
}
ORDER BY DESC(?revenueBillions)
